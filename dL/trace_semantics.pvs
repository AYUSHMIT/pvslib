trace_semantics : THEORY
BEGIN

  IMPORTING HP,
            hp_expr,
            TraceState,
            ODEs_equiv,
	          structures@more_list_props,
	          bounded_star_semantics,
            substitution

  wf_trace?(trace:list[TraceState]) : bool =
    cons?(trace) AND
    FORALL (i:below(length(trace)-1)) :
      state?(nth(trace,i)) OR state_diff?(nth(trace,i))

  Trace : TYPE = (wf_trace?)

  % @QED wf_append proved by ltitolo on Fri, 07 Apr 2023 15:46:58 GMT
  wf_append : LEMMA
    FORALL (trace1,trace2:Trace):
      last(trace1) = car(trace2) IMPLIES wf_trace?(append(trace1, cdr(trace2)))

  finite_trace?(trace:Trace) : bool =
    state?(last(trace)) OR state_diff?(last(trace))

  final_env(trace:(finite_trace?)): Environment =
    IF state?(last(trace))
      THEN state(last(trace))
      ELSE behavior(last(trace))(max(D(last(trace))))
    ENDIF

  FiniteTrace : TYPE = (finite_trace?)

  lastState(trace:FiniteTrace): Environment = state(last(trace))

  trace_semantic_rel(hp:HP)(trace:Trace) : INDUCTIVE bool =
    (assign?(hp) AND length(trace) = 2 AND
		     LET envi = nth(trace,0),
		         envo = nth(trace,1),
		         l = assigns(hp) IN
		     state?(envi) AND
		     state?(envo) AND
            ((FORALL (i:below(length(l))) :
              LET (vari,rexpr) = nth(l,i) IN
                state(envo)(vari) = rexpr(state(envi))) AND
		            FORALL (varj:(not_in_map(l))) :
                state(envo)(varj) = state(envi)(varj))
    )
    OR
    (diff?(hp) AND 
      (length(trace) = 1 AND
        LET env = nth(trace,0) IN
        ((state?(env) AND be(hp)(state(env)))
        OR
        (state_diff?(env) AND
          EXISTS(fs:(solution_odes_u?(D(env),odes(hp),s0(env)))):
            (behavior(env) = (LAMBDA (r:(D(env))):
              env_at_t(odes(hp),fs,s0(env))(r))
            AND
            FORALL(t:(D(env))): be(hp)(behavior(env)(t))))
        %       EXISTS(D:(dd?)):
	      %           semantic_rel_diff(D,odes(hp),be(hp),state(envi),state(envo)))
        OR
        (inf_diff?(env) AND
         EXISTS(fs:(solution_odes_u?(hp(0),odes(hp),s0(env)))):
         (inf_behavior(env) = (LAMBDA (r:(hp(0))):
           env_at_t(odes(hp),fs,s0(env))(r))
         AND
         FORALL(t:(hp(0))): be(hp)(inf_behavior(env)(t))))))
        OR
        (length(trace)=2 AND 
          LET envi = nth(trace,0),
              envo = nth(trace,1) IN
              state?(envi) AND
          error?(envo) AND 
          NOT be(hp)(state(envi)))
    )
    OR
    (any?(hp) AND
      length(trace) = 2 AND
		  LET envi = nth(trace,0),
		      envo = nth(trace,1) IN
		  (state?(envi) AND
		   state?(envo) AND
		  (EXISTS(r:real): anyset(hp)(r)(state(envi)) AND state(envo)(anyvar(hp)) = r) AND
    	      	      FORALL (i:nat | i /= anyvar(hp)) : state(envo)(i) = state(envi)(i))
    )
    OR
    (test?(hp) AND
      ((length(trace)=1
        AND
        LET envi = nth(trace,0) IN
		      state?(envi) AND
		      be(hp)(state(envi)))
        OR
        (length(trace)=1
        AND
        % not sure about these two
        % a diff is always preceeded by an env in our current semantics
        LET envi = nth(trace,0) IN
		      inf_diff?(envi) AND
          FORALL (r:(hp(0))): be(hp)(inf_behavior(envi)(r)))
        OR
        (length(trace)=1
        AND
        LET envi = nth(trace,0) IN
		      state_diff?(envi) AND
          FORALL (r:(D(envi))): be(hp)(behavior(envi)(r)))
        %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
        OR
		    (length(trace)=2 AND
		      LET envi = nth(trace,0),
		          envo = nth(trace,1) IN
		      not be(hp)(state(envi)) AND
		      state?(envi) AND
		      error?(envo)
		     ))
    )
    OR
    (seq?(hp) AND
      ((EXISTS (trace1, trace2:Trace):
        (trace = append(trace1,cdr(trace2)) AND
         trace_semantic_rel(stm1(hp))(trace1) AND
			   trace_semantic_rel(stm2(hp))(trace2) AND
			   state?(last(trace1)) AND
			   state?(car(trace2)) AND
			   state(last(trace1))=state(car(trace2))))
        OR
        (EXISTS (trace1, trace2:Trace):
        (trace = append(trace1,cdr(trace2)) AND
         trace_semantic_rel(stm1(hp))(trace1) AND
			   trace_semantic_rel(stm2(hp))(trace2) AND
			   state_diff?(last(trace1)) AND
			   state?(car(trace2)) AND
         behavior(last(trace1))(max(D(last(trace1))))=state(car(trace2))))
        OR
        (EXISTS (trace1, trace2:Trace):
        (trace = append(trace1,trace2) AND
         trace_semantic_rel(stm1(hp))(trace1) AND
			   trace_semantic_rel(stm2(hp))(trace2) AND
			   state?(last(trace1)) AND
			   state_diff?(car(trace2)) AND
         state(last(trace1)) = s0(car(trace2))))
        OR
        (EXISTS (trace1, trace2:Trace):
        (trace = append(trace1,trace2) AND
         trace_semantic_rel(stm1(hp))(trace1) AND
			   trace_semantic_rel(stm2(hp))(trace2) AND
			   state?(last(trace1)) AND
			   inf_diff?(car(trace2)) AND
         state(last(trace1)) = s0(car(trace2))))
		    OR
			  ((error?(last(trace)) OR inf_diff?(last(trace))) AND
		        trace_semantic_rel(stm1(hp))(trace)))
    )
    OR
    (union?(hp) AND
      (trace_semantic_rel(stm1(hp))(trace)
        OR
		   trace_semantic_rel(stm2(hp))(trace))
    )
    OR
    (star?(hp) AND
      ((length(trace) = 1 AND state?(car(trace)))
      OR
		  (EXISTS (trace1, trace2:Trace):
		     trace = append(rdc(trace1),trace2) AND
		     trace_semantic_rel(stm(hp))(trace1) AND
         trace_semantic_rel(hp)(trace2) AND
		     state?(last(trace1)) AND
		     state?(car(trace2)) AND
		     state(car(trace2)) = state(last(trace1)))
      OR
		  (EXISTS (trace1, trace2:Trace):
		     trace = append(rdc(trace1),trace2) AND
		     trace_semantic_rel(stm(hp))(trace1) AND
         trace_semantic_rel(hp)(trace2) AND
		     state_diff?(last(trace1)) AND
		     state?(car(trace2)) AND
		     behavior(last(trace1))(max(D(last(trace1))))=state(car(trace2)))
      % now we don't need the state - state_diff case since a trace cannot begin with state_diff
      OR
			((error?(last(trace)) OR inf_diff?(last(trace))) AND
		        trace_semantic_rel(stm(hp))(trace)))
    )

  exists_trace_env: LEMMA
    FORALL (hp:HP, env:Environment):
      EXISTS (trace:Trace): trace_semantic_rel(hp)(trace)
                            AND
                            (state?(car(trace)) IMPLIES state(car(trace)) = env)

%--------------------------------
% ---     SOUNDNESS           ---
%--------------------------------

  % @QED trace_semantics_sound_assign proved by ltitolo on Tue, 28 Mar 2023 01:40:58 GMT
  trace_semantics_sound_assign: LEMMA
    FORALL(trace:FiniteTrace,assigns:Assigns) :
      LET envi = state(car(trace)),
          envo = final_env(trace) IN
      trace_semantic_rel(ASSIGN(assigns))(trace)
      IMPLIES
      semantic_rel(ASSIGN(assigns))(envi)(envo)

  % @QED trace_semantics_sound_diff proved by ltitolo on Tue, 28 Mar 2023 01:41:08 GMT
  trace_semantics_sound_diff: LEMMA
    FORALL(trace:FiniteTrace,odes:ODEs,be:BoolExpr) :
      LET envi = state(car(trace)),
          envo = final_env(trace) IN
      trace_semantic_rel(DIFF(odes,be))(trace)
      IMPLIES
      semantic_rel(DIFF(odes,be))(envi)(envo)

  % @QED trace_semantics_sound_any proved by ltitolo on Tue, 28 Mar 2023 01:40:13 GMT
  trace_semantics_sound_any: LEMMA
    FORALL(trace:FiniteTrace,anyvar:nat,anyset:[real->BoolExpr]) :
      LET envi = state(car(trace)),
          envo = final_env(trace) IN
      trace_semantic_rel(ANY(anyvar,anyset))(trace)
      IMPLIES
      semantic_rel(ANY(anyvar,anyset))(envi)(envo)

  % @QED trace_semantics_sound_test proved by ltitolo on Tue, 28 Mar 2023 01:40:41 GMT
  trace_semantics_sound_test: LEMMA
    FORALL(trace:FiniteTrace,be:BoolExpr) :
      LET envi = state(car(trace)),
          envo = final_env(trace) IN
      trace_semantic_rel(TEST(be))(trace)
      IMPLIES
      semantic_rel(TEST(be))(envi)(envo)

  % @QED trace_semantics_sound_seq proved by ltitolo on Tue, 28 Mar 2023 01:39:55 GMT
  trace_semantics_sound_seq: LEMMA
    FORALL (stm1,stm2: HP):
      ((FORALL(trace:FiniteTrace) :
          LET envi = state(car(trace)),
              envo = final_env(trace) IN
          trace_semantic_rel(stm1)(trace)
        IMPLIES semantic_rel(stm1)(envi)(envo))
        AND
        (FORALL(trace:FiniteTrace) :
          LET envi = state(car(trace)),
              envo = final_env(trace) IN
          trace_semantic_rel(stm2)(trace)
        IMPLIES semantic_rel(stm2)(envi)(envo)))
       IMPLIES
       FORALL(trace:FiniteTrace) :
        LET envi = state(car(trace)),
            envo = final_env(trace) IN
        trace_semantic_rel(SEQ(stm1,stm2))(trace)
        IMPLIES semantic_rel(SEQ(stm1,stm2))(envi)(envo)

  % @QED trace_semantics_sound_union proved by ltitolo on Tue, 28 Mar 2023 01:51:27 GMT
  trace_semantics_sound_union: LEMMA
    FORALL (stm1,stm2: HP):
      ((FORALL(trace:FiniteTrace) :
          LET envi = state(car(trace)),
              envo = final_env(trace) IN
          trace_semantic_rel(stm1)(trace)
        IMPLIES semantic_rel(stm1)(envi)(envo))
        AND
        (FORALL(trace:FiniteTrace) :
          LET envi = state(car(trace)),
              envo = final_env(trace) IN
          trace_semantic_rel(stm2)(trace)
        IMPLIES semantic_rel(stm2)(envi)(envo)))
       IMPLIES
       FORALL(trace:FiniteTrace) :
        LET envi = state(car(trace)),
            envo = final_env(trace) IN
        trace_semantic_rel(UNION(stm1,stm2))(trace)
        IMPLIES semantic_rel(UNION(stm1,stm2))(envi)(envo)

  % @QED trace_semantics_sound_star proved by ltitolo on Sat, 01 Apr 2023 15:17:28 GMT
  trace_semantics_sound_star : LEMMA
  FORALL (hp:HP) :
     ((FORALL (trace:FiniteTrace):
        trace_semantic_rel(hp)(trace)
        IMPLIES
        semantic_rel(hp)(state(car(trace)))(final_env(trace)))
      IMPLIES
     (FORALL (trace:FiniteTrace):
        trace_semantic_rel(STAR(hp))(trace)
        IMPLIES
        EXISTS (i:nat) : semantic_rel_bounded_star(i)(STAR(hp))(state(car(trace)))(final_env(trace))
     ))

  % @QED trace_semantics_sound proved by ltitolo on Tue, 28 Mar 2023 14:16:41 GMT
  trace_semantics_sound: THEOREM
    FORALL(hp:HP) :
    FORALL(trace:FiniteTrace) :
      LET envi = state(car(trace)),
          envo = final_env(trace)
      IN
        trace_semantic_rel(hp)(trace) IMPLIES semantic_rel(hp)(envi)(envo)


%--------------------------------
% ---     COMPLETENESS        ---
%--------------------------------

  % @QED trace_semantics_complete_assign proved by ltitolo on Tue, 28 Mar 2023 02:07:26 GMT
  trace_semantics_complete_assign: LEMMA
    FORALL (envi,envo: Environment, assigns:Assigns) :
      semantic_rel(ASSIGN(assigns))(envi)(envo)
      IMPLIES
      EXISTS(trace:FiniteTrace):
        envi = state(car(trace)) AND
        envo = final_env(trace) AND
        trace_semantic_rel(ASSIGN(assigns))(trace)

  % @QED trace_semantics_complete_diff proved by ltitolo on Wed, 29 Mar 2023 18:27:26 GMT
  trace_semantics_complete_diff: LEMMA
    FORALL (envi,envo: Environment, odes:ODEs,be:BoolExpr) :
      semantic_rel(DIFF(odes,be))(envi)(envo)
      IMPLIES
      EXISTS(trace:FiniteTrace):
        envi = state(car(trace)) AND
        envo = final_env(trace) AND
        trace_semantic_rel(DIFF(odes,be))(trace)

  % @QED trace_semantics_complete_any proved by ltitolo on Sat, 01 Apr 2023 15:26:14 GMT
  trace_semantics_complete_any: LEMMA
    FORALL (envi,envo: Environment, anyvar:nat,anyset:[real->BoolExpr]) :
      semantic_rel(ANY(anyvar,anyset))(envi)(envo)
        IMPLIES
        EXISTS(trace:FiniteTrace):
          envi = state(car(trace)) AND
          envo = final_env(trace) AND
          trace_semantic_rel(ANY(anyvar,anyset))(trace)

  % @QED trace_semantics_complete_test proved by ltitolo on Mon, 03 Apr 2023 14:12:57 GMT
  trace_semantics_complete_test: LEMMA
    FORALL (envi,envo: Environment, be:BoolExpr) :
      semantic_rel(TEST(be))(envi)(envo)
      IMPLIES
      EXISTS(trace:FiniteTrace):
        envi = state(car(trace)) AND
        envo = final_env(trace) AND
        trace_semantic_rel(TEST(be))(trace)

  % @QED trace_semantics_complete_seq proved by ltitolo on Sun, 02 Apr 2023 02:15:27 GMT
  trace_semantics_complete_seq : LEMMA
    FORALL (stm1,stm2: HP):
      ((FORALL (envi, envo: Environment):
          semantic_rel(stm1)(envi)(envo) IMPLIES
          EXISTS (trace: FiniteTrace):
                  envi = state(car(trace)) AND
                  envo = final_env(trace) AND
                  trace_semantic_rel(stm1)(trace))
        AND
        FORALL (envi, envo: Environment):
          semantic_rel(stm2)(envi)(envo) IMPLIES
          EXISTS (trace: FiniteTrace):
                  envi = state(car(trace)) AND
                  envo = final_env(trace) AND
                  trace_semantic_rel(stm2)(trace))
       IMPLIES
       FORALL (envi, envo: Environment):
         semantic_rel(SEQ(stm1, stm2))(envi)(envo) IMPLIES
          EXISTS (trace: FiniteTrace):
                  envi = state(car(trace)) AND
                  envo = final_env(trace) AND
                  trace_semantic_rel(SEQ(stm1, stm2))(trace)

  % @QED trace_semantics_complete_union proved by ltitolo on Wed, 29 Mar 2023 18:36:51 GMT
  trace_semantics_complete_union : LEMMA
    FORALL (stm1,stm2: HP):
      ((FORALL (envi, envo: Environment):
          semantic_rel(stm1)(envi)(envo) IMPLIES
          EXISTS (trace: FiniteTrace):
                  envi = state(car(trace)) AND
                  envo = final_env(trace) AND
                  trace_semantic_rel(stm1)(trace))
        AND
        FORALL (envi, envo: Environment):
          semantic_rel(stm2)(envi)(envo) IMPLIES
          EXISTS (trace: FiniteTrace):
                  envi = state(car(trace)) AND
                  envo = final_env(trace) AND
                  trace_semantic_rel(stm2)(trace))
        IMPLIES
        FORALL (envi, envo: Environment):
          semantic_rel(UNION(stm1, stm2))(envi)(envo) IMPLIES
          EXISTS (trace: FiniteTrace):
                  envi = state(car(trace)) AND
                  envo = final_env(trace) AND
                  trace_semantic_rel(UNION(stm1, stm2))(trace)

  % @QED trace_semantics_complete_star proved by ltitolo on Fri, 07 Apr 2023 02:55:54 GMT
  trace_semantics_complete_star : LEMMA
    FORALL (hp: HP) :
        (FORALL (envi, envo: Environment):
            semantic_rel(hp)(envi)(envo) IMPLIES
            EXISTS (trace: FiniteTrace):
                    envi = state(car(trace)) AND
                    envo = final_env(trace) AND
                    trace_semantic_rel(hp)(trace))
         IMPLIES
         FORALL (envi, envo: Environment, i: nat):
           semantic_rel_bounded_star(i)(STAR(hp))(envi)(envo) IMPLIES
            EXISTS (trace: FiniteTrace):
                    envi = state(car(trace)) AND
                    envo = final_env(trace) AND
                    trace_semantic_rel(STAR(hp))(trace)

  % @QED trace_semantics_complete proved by ltitolo on Thu, 06 Apr 2023 19:04:45 GMT
  trace_semantics_complete: THEOREM
    FORALL(hp:HP) :
    FORALL (envi, envo : Environment) :
      semantic_rel(hp)(envi)(envo)
      IMPLIES
      EXISTS(trace:FiniteTrace):
        envi = state(car(trace)) AND
        envo = final_env(trace) AND
        trace_semantic_rel(hp)(trace)

  END trace_semantics
