trace_semantics : THEORY
BEGIN

  IMPORTING HP,
            hp_expr,
            TraceState,
            ODEs_equiv,
	          structures@more_list_props,
	          bounded_star_semantics

  wf_trace?(trace:list[TraceState]) : bool =
    cons?(trace) AND
    FORALL (i:below(length(trace)-1)) :
      state?(nth(trace,i))

  Trace : TYPE = (wf_trace?)

  finite_trace?(trace:Trace) : bool =
    state?(last(trace))

  FiniteTrace : TYPE = (finite_trace?)

  lastState(trace:FiniteTrace) : Environment = state(last(trace))

  trace_semantic_rel(hp:HP)(trace:Trace) : INDUCTIVE bool =
    (assign?(hp) AND length(trace) = 2 AND
		     LET envi = nth(trace,0),
		         envo = nth(trace,1),
		         l = assigns(hp) IN
		     state?(envi) AND
		     state?(envo) AND
                     ((FORALL (i:below(length(l))) :
                        LET (vari,rexpr) = nth(l,i) IN
                        state(envo)(vari) = rexpr(state(envi))) AND
		      FORALL (varj:(not_in_map(l))) :state(envo)(varj) = state(envi)(varj))
    )
    OR
    (diff?(hp) AND
      (length(trace) = 2 AND
		    LET envi = nth(trace,0),
		        envo = nth(trace,1) IN
		     ((state?(envi) AND
		       state?(envo) AND
             EXISTS(D:(dd?)):
	               semantic_rel_diff(D,odes(hp),be(hp),state(envi),state(envo))))
		      OR
		      (state?(envi) AND error?(envo) AND not be(hp)(state(envi))))
      OR
      (length(trace) = 2 AND
        LET envi = nth(trace,0),
            envo = nth(trace,1) IN
            state?(envi) AND
            inf_diff?(envo) AND
            EXISTS(fs:(solution_odes_u?(hp(0),odes(hp),state(envi)))):
            behavior(envo) = (LAMBDA (r:(hp(0))):
              env_at_t(odes(hp),fs,state(envi))(r)))
    )
    OR
    (test?(hp) AND
      ((length(trace)=1
        AND
        LET envi = nth(trace,0) IN
		      state?(envi) AND
		      be(hp)(state(envi)))
        OR
        (length(trace)=1
        AND
        LET envi = nth(trace,0) IN
		      inf_diff?(envi) AND
          FORALL (r:(hp(0))): be(hp)(behavior(envi)(r)))
        OR
		    (length(trace)=2 AND
		      LET envi = nth(trace,0),
		          envo = nth(trace,1) IN
		      not be(hp)(state(envi)) AND
		      state?(envi) AND
		      error?(envo)
		     ))
    )
    OR
    (seq?(hp) AND
      (EXISTS (trace1, trace2:Trace):
        (trace = append(trace1,cdr(trace2)) AND
         trace_semantic_rel(stm1(hp))(trace1) AND
			   trace_semantic_rel(stm2(hp))(trace2) AND
			   state?(last(trace1)) AND
			   state?(car(trace2)) AND
			   state(last(trace1))=state(car(trace2))))
		    OR
			  ((error?(last(trace)) OR inf_diff?(last(trace))) AND
		        trace_semantic_rel(stm1(hp))(trace))
    )
    OR
    (union?(hp) AND
      (trace_semantic_rel(stm1(hp))(trace)
        OR
		   trace_semantic_rel(stm2(hp))(trace))
    )
    OR
    (star?(hp) AND
      ((length(trace) = 1 AND state?(car(trace)))
      OR
		  (EXISTS (trace1, trace2:Trace):
		     trace = append(trace1,cdr(trace2)) AND
		     trace_semantic_rel(stm(hp))(trace1) AND
         trace_semantic_rel(hp)(trace2) AND
		     state?(last(trace1)) AND
		     state?(car(trace2)) AND
		     state(car(trace2)) = state(last(trace1)))
      OR
			((error?(last(trace)) OR inf_diff?(last(trace))) AND
		        trace_semantic_rel(stm(hp))(trace)))
    )
    OR
    (any?(hp) AND
      length(trace) = 2 AND
		  LET envi = nth(trace,0),
		      envo = nth(trace,1) IN
		  (state?(envi) AND
		   state?(envo) AND
		  (EXISTS(r:real): anyset(hp)(r)(state(envi)) AND state(envo)(anyvar(hp)) = r) AND
    	      	      FORALL (i:nat | i /= anyvar(hp)) : state(envo)(i) = state(envi)(i))
    )

  trace_semantics_complete_seq : LEMMA
    FORALL (SEQ1_var: HP, SEQ2_var: HP):
        ((FORALL (envi, envo: Environment):
            semantic_rel(SEQ1_var)(envi)(envo) IMPLIES
             EXISTS (trace: Trace):
                    (NOT null?(trace)) AND state?(car(trace))
                AND state?(last(trace)) AND (envi = state(car(trace)))
                AND (envo = state(last(trace)))
                AND trace_semantic_rel(SEQ1_var)(trace))
          AND
          FORALL (envi, envo: Environment):
            semantic_rel(SEQ2_var)(envi)(envo) IMPLIES
             EXISTS (trace: Trace):
                    (NOT null?(trace)) AND state?(car(trace))
                AND state?(last(trace)) AND (envi = state(car(trace)))
                AND (envo = state(last(trace)))
                AND trace_semantic_rel(SEQ2_var)(trace))
         IMPLIES
         FORALL (envi, envo: Environment):
           semantic_rel(SEQ(SEQ1_var, SEQ2_var))(envi)(envo) IMPLIES
            EXISTS (trace: Trace):
                   (NOT null?(trace)) AND state?(car(trace))
               AND state?(last(trace)) AND (envi = state(car(trace)))
               AND (envo = state(last(trace)))
               AND trace_semantic_rel(SEQ(SEQ1_var, SEQ2_var))(trace)


  trace_sem_rel_implies_bounded : LEMMA
  FORALL (hp: HP) :
     (FORALL (trace: FiniteTrace):
        (NOT null?(trace)) AND state?(car(trace))
         AND state?(last(trace))
         AND trace_semantic_rel(hp)(trace)
       IMPLIES
         semantic_rel(hp)(state(car(trace)))(state(last(trace))))
      IMPLIES
     (FORALL (trace: FiniteTrace):
        (NOT null?(trace)) AND state?(car(trace))
         AND state?(last(trace))
         AND trace_semantic_rel(STAR(hp))(trace)
       IMPLIES
        EXISTS (i:nat) : semantic_rel_bounded_star(i)(STAR(hp))(state(car(trace)))(state(last(trace)))
     )

  semantic_rel_bounded_implies_trace_semantic_rel : LEMMA
    FORALL (hp: HP) :
        (FORALL (envi, envo: Environment):
            semantic_rel(hp)(envi)(envo) IMPLIES
             EXISTS (trace: Trace):
                    (NOT null?(trace)) AND state?(car(trace))
                AND state?(last(trace)) AND (envi = state(car(trace)))
                AND (envo = state(last(trace)))
                AND trace_semantic_rel(hp)(trace))
         IMPLIES
         FORALL (envi, envo: Environment, i: nat):
           semantic_rel_bounded_star(i)(STAR(hp))(envi)(envo) IMPLIES
            EXISTS (trace: Trace):
                   (NOT null?(trace)) AND state?(car(trace))
               AND state?(last(trace)) AND envi = state(car(trace))
               AND envo = state(last(trace))
               AND trace_semantic_rel(STAR(hp))(trace)

  trace_semantics_sound: LEMMA
     FORALL(hp:HP) :
     FORALL(trace:FiniteTrace) : not null?(trace) AND state?(car(trace)) AND state?(last(trace)) IMPLIES
        LET envi=state(car(trace)),
            envo=state(last(trace)) IN
        trace_semantic_rel(hp)(trace) IMPLIES semantic_rel(hp)(envi)(envo)

  trace_semantics_complete: LEMMA
     FORALL(hp:HP) :
     FORALL (envi, envo : Environment) :
       semantic_rel(hp)(envi)(envo) IMPLIES
         EXISTS(trace:Trace) : not null?(trace) AND state?(car(trace)) AND state?(last(trace)) AND
                               envi=state(car(trace)) AND envo=state(last(trace)) AND trace_semantic_rel(hp)(trace)


  END trace_semantics
